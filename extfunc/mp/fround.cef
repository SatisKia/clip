#!NAMESPACE mp
#\!- <a> <prec> [<mode>]
:include !mp:fround
:include !mp:frmode

:params &a[] prec mode
:ans FALSE

$SUB_A	@n,mpf_get_prec a,prec
$RETURN	(@n < 1)
$SET	@a,mp_fround_get a (@n - 1)
#$SET_F	@u
#$SET_F	@U

if @! < 3 # パラメータが2つの場合
	$SET mode,MP_FROUND_HALF_EVEN
endif
switch mode
case MP_FROUND_UP
	$SET_T @U
	breakswi
case MP_FROUND_DOWN
	breakswi
case MP_FROUND_CEILING
	$SET @U,(a 0 > 0)
	breakswi
case MP_FROUND_FLOOR
	$SET @U,(a 0 < 0)
	breakswi
case MP_FROUND_HALF_UP
	$SET @u,(@a > 4)
	breakswi
case MP_FROUND_HALF_DOWN
	$SET @u,(@a > 5)
	breakswi
case MP_FROUND_HALF_EVEN
	if (mp_fround_get a @n) % 2 == 1
		$SET @u,(@a > 4)
	else
		$SET @u,(@a > 5)
	endif
	breakswi
endswi

if @U
	if @a > 0
		$SET_T @u
	elif @n > 1
		$SET @u,mp_fround_isnotzero a (@n - 2)
	endif
endif

if @u
	mp_fround_zero a @n
	mp_fround_up a @n
else
	mp_fround_zero a (@n - 1)
	mp_fround_set a (@n - 1) 0
endif
