#Chudnovsky(チャドノフスキー)のアルゴリズム
#\!- <digits>

func computePQT
	:params &P &Q &T n1 n2
	:parent @A A
	:parent @B B
	:parent @C C
	if n1 + 1 == n2
		P = [-](2 * n2 - 1) * (6 * n2 - 5) * (6 * n2 - 1)
		Q = n2 ** 3 * C ** 3 / 24
		T = (A + B * n2) * P
	else
		:var P1 Q1 T1 P2 Q2 T2
		@m = int ((n1 + n2) / 2)
		computePQT P1 Q1 T1 n1 @m
		computePQT P2 Q2 T2 @m n2
		P = P1 * P2
		Q = Q1 * Q2
		T = T1 * Q2 + P1 * T2
	endif
endfunc

@n = int (@0 / 14)
if @n <= 0; @n = 1; endif
@A = 13591409
@B = 545140134
@C = 640320
computePQT @P @Q @T 0 @n
:var pi
pi = sqrt @C
pi *= @C * @Q
pi /= (@T + @A * @Q) * 12
return pi
