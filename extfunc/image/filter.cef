#!NAMESPACE image
#フィルター処理
#\!- <in_array> <matrix> <out_array>
:include !image:color

func filter y x h w
	:warn FALSE
	:global filter_src[] filter_mat[]
#	$SET_Z		@r
#	$SET_Z		@g
#	$SET_Z		@b
#	$SET_Z		@y
	$LOOPSTART	# @y
	$ADD_A		@Y,y,@y
	$SET_Z		@x
	$LOOPSTART	# @x
	$SET		@c,filter_src @Y (x + @x)
	$SET		@k,filter_mat @y @x
	$MUL_A		@t,get_red @c,@k
	$ADD		@r,@t
	$MUL_A		@t,get_green @c,@k
	$ADD		@g,@t
	$MUL_A		@t,get_blue @c,@k
	$ADD		@b,@t
	$LOOPEND_I	@x,w
	$LOOPEND_I	@y,h
	return make_color_s @r @g @b # 飽和演算
endfunc

:ans FALSE
:param 2 TRUE

:global filter_src[] filter_mat[]
$SET		filter_src,@@0
$SET		filter_mat,@@1
$COL		@W,filter_mat
$ROW		@H,filter_mat
$SET		@X,int (@W / 2)
$SET		@Y,int (@H / 2)

$COL		@w,filter_src
$ROW		@h,filter_src
$LOOPSTART	# @y
$SET_Z		@x
$LOOPSTART	# @x
$SET		@@2 @y @x,filter (@y - @Y) (@x - @X) @H @W
$LOOPEND_I	@x,@w
$LOOPEND_I	@y,@h

:undef filter_src
:undef filter_mat
