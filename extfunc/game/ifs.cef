#!/usr/local/bin/clip
#IFS (iterated function system)
#\!- [<array_data>] [<width> <height>]
:params data[] width height
:ans 0

if @! != 3
	if @! == 2
		height = @1
		width  = @0
	endif
	if @! != 1
		!ifsm data
	endif
	if @! != 2
		width  = 640; :scan ["グラフィックの幅? ]   width
		height = 480; :scan ["グラフィックの高さ? ] height
	endif
endif

:var left bottom right top

left   = data 0
bottom = data 1
right  = data 2
top    = data 3
@N     = data 4
for @i = 0; @i < @N; @i++
	@@a @i = data (@i + 5         )
	@@b @i = data (@i + 5 + @N    )
	@@c @i = data (@i + 5 + @N * 2)
	@@d @i = data (@i + 5 + @N * 3)
	@@e @i = data (@i + 5 + @N * 4)
	@@f @i = data (@i + 5 + @N * 5)
next

@M = 25 * @N

#
# 確率の計算
#
@s = 0
for @i = 0; @i < @N; @i++
	@@p @i = abs (@@a @i * @@d @i - @@b @i * @@c @i)
	@s += @@p @i; @@i @i = @i
next

#
# 整列
#
for @i = 0; @i < @N - 1; @i++
	@k = @i
	for @j = @i + 1; @j < @N; @j++
		if @@p @j < @@p @k; @k = @j; endif
	next
	@t = @@p @i; @@p @i = @@p @k; @@p @k = @t
	@r = @@i @i; @@i @i = @@i @k; @@i @k = @r
next

#
# 表作成
#
@r = @M
for @i = 0; @i < @N; @i++
	@k = int (@r * @@p @i / @s + 0.5); @s -= @@p @i
	do; @@t ([--]@r) = @@i @i; until [--]@k > 0
next

:gworld width height
:window left bottom right top
:gupdate 0

#
# IFSのアトラクタをプロット
#
@x = @y = 0
for @i = 0; @i < 30000; @i++
	@j = @@t (rand / (RAND_MAX / @M + 1))
	@t = @@a @j * @x + @@b @j * @y + @@e @j
	@y = @@c @j * @x + @@d @j * @y + @@f @j
	@x = @t
	if @i >= 10; :wput @x @y 255; endif
next

:gupdate \-1
